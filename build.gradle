plugins {
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.palantir.git-version' version '0.12.2'
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
}

repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenCentral()
}

group = 'com.newcubator'
version = gitVersion()

sourceCompatibility = JavaVersion.VERSION_11

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'

    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'org.springframework:spring-jdbc:5.3.0'

    testRuntime 'com.h2database:h2:1.4.200'

    // poi excel export
    implementation 'org.apache.poi:poi:4.1.2'
    implementation 'org.apache.poi:poi-ooxml:4.1.2'

    // Use the awesome Spock testing and specification framework
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'org.spockframework:spock-spring:1.3-groovy-2.5'

    // Use spring to for integration testing
    testImplementation 'org.springframework.boot:spring-boot-starter-jdbc:2.3.5.RELEASE'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.3.5.RELEASE'

    testImplementation 'org.postgresql:postgresql:42.2.5'

    testCompile "org.testcontainers:spock:1.15.2"
    testCompile "org.testcontainers:postgresql:1.15.2"
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        JdbcExcel(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.newcubator'
            artifactId 'jdbc-excel'
            version project.getVersion()
            pom {
                name = 'jdbc-excel'
                description = 'A library to create excel from jdbc'
                url = 'https://github.com/newcubator/jdbc-excel'
                licenses {
                    license {
                        name = "MIT License"
                        url =  "http://www.opensource.org/licenses/mit-license.php"
                    }
                }
                developers {
                    developer {
                        id = "terrible_herbst"
                        name = "Joerg Herbst"
                        email = "joerg.herbst@newcubator.de"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/newcubator/jdbc-excel.git"
                    developerConnection = "scm:git:ssh://git@github.com:newcubator/jdbc-excel.git"
                    url = "https://www.github.com/newcubator/jdbc-excel"
                }
            }
        }
    }
    repositories {
        maven {
            def snapshotRepoUrl =  "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            def releaseRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            url = version.matches("\\d\\.\\d\\.\\d") ? releaseRepoUrl : snapshotRepoUrl
            credentials {
                username System.getenv("SONATYPE_USER")
                password System.getenv("SONATYPE_PASSWORD")
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("SONATYPE_USER")
            password = System.getenv("SONATYPE_PASSWORD")
        }
    }
}

signing {
    def signingKey = System.getenv("SIGNING_KEY")
    def signingPassword = System.getenv("SIGNING_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.JdbcExcel
}
