plugins {
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.4"
    id 'com.palantir.git-version' version '0.12.2'
}

repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenCentral()
}

group = 'com.newcubator'
version = gitVersion()

sourceCompatibility = JavaVersion.VERSION_11

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'

    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'org.springframework:spring-jdbc:5.3.0'

    testRuntime 'com.h2database:h2:1.4.200'

    // poi excel export
    implementation 'org.apache.poi:poi:4.1.2'
    implementation 'org.apache.poi:poi-ooxml:4.1.2'

    // Use the awesome Spock testing and specification framework
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'org.spockframework:spock-spring:1.3-groovy-2.5'

    // Use spring to for integration testing
    testImplementation 'org.springframework.boot:spring-boot-starter-jdbc:2.3.5.RELEASE'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.3.5.RELEASE'

    testImplementation 'org.postgresql:postgresql:42.2.5'

    testCompile "org.testcontainers:spock:1.15.2"
    testCompile "org.testcontainers:postgresql:1.15.2"
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
        }
    }
    developers {
        developer {
            id "terrible_herbst"
            name "Joerg Herbst"
            email "joerg.herbst@newcubator.de"
        }
    }

    scm {
        url "https://github.com/newcubator/jdbc-excel"
    }
}

publishing {
    publications {
        JdbcExcelPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.newcubator'
            artifactId 'jdbc-excel-converter'
            version project.getVersion()
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A library to create excel from jdbc')
                root.appendNode('name', 'jdbc-excel-converter')
                root.appendNode('url', 'https://github.com/newcubator/jdbc-excel')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['JdbcExcelPublication']
    pkg {
        repo = 'jdbc-excel-converter'
        name = 'jdbc-excel-converter'
        userOrg = 'newcubator'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/newcubator/jdbc-excel'
        version {
            name = project.getVersion()
            desc = 'A library to create excel from jdbc'
            released = new Date()
            vcsTag = project.getVersion()
        }
    }
}
